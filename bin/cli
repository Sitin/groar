#!/usr/bin/env coffee

program = require 'commander'
fs = require 'fs'
path = require 'path'
wrench = require 'wrench'
pckg = require "#{__dirname}/../package.json"
defaultTemplate = 'module'

program
  .version(pckg.version)
  .option('-i, --init [dir_name]', 'Init directory')
  .option('-t, --template [template]', 'Set template')
  .parse(process.argv)

if program.init
  project =
    template: program.template
    name:     program.init

  # Only one project creation allowed per project:
  if project.name instanceof Array
    project.name = project.name[0]

  # Project defaults:
  if typeof project.name == 'string'
    project.path = './' + project.name
  else
    project.name = path.basename '.'
    project.path = path.dirname '.'

  # Template defaults:
  if typeof project.template != 'string'
    project.template = defaultTemplate

  project.templatePath = "#{__dirname}/../templates/#{project.template}/"

  # Report accepted command:
  console.log "Init with template #{project.template} to '#{project.path}'."

  # Copy template dir:
  wrench.copyDirRecursive project.templatePath, project.path, (err, current) ->
    if not err
      msg = [
        'Done. Now try:',
        '    $ cd %s',
        '    $ npm install',
        '    $ grunt watch'].join '\n'
      console.log msg, project.name
    current unless err
else
  do program.help
